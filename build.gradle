plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.7'
}

group = 'info.preva1l'
version = '1.0'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

dependencies {
    implementation("net.dv8tion:JDA:5.1.2") {
        exclude module: 'opus-java'
    }

    implementation 'redis.clients:jedis:5.2.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.mongodb:mongodb-driver-sync:5.0.0'
    implementation "org.bspfsystems:yamlconfiguration:3.0.1"

    implementation 'org.jetbrains:annotations:24.0.0'
    implementation 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.jetbrains:annotations:24.0.0'
}

def targetJavaVersion = 21

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: rootProject.version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('config.yml') {
        expand props
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'info.preva1l.slave.Main'
        )
    }
}

shadowJar {
    relocate 'org.mongodb', 'info.preva1l.slave.libs.mongo'
    relocate 'org.bspfsystems', 'info.preva1l.slave.libs.config'

    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')
    minimize()
}

jar.dependsOn shadowJar
clean.delete "$rootDir/target"

logger.lifecycle("Building Slave ${version}")